const LOCALID_SEEL_COPYER = 3
const LOCALID_SEEL_COPYEE = 4
const LOCALID_GRUNT = 8

mapscripts Aquarium_Window_MapScripts {}

script Aquarium_Window_SeelCopyee {
	lockall
	specialvar(VAR_RESULT, GetPlayerFacingDirection)
	if (var(VAR_RESULT) == DIR_EAST) {
		applymovement(LOCALID_SEEL_COPYER, Aquarium_Window_SeelCopyee_SeelRight)
		applymovement(LOCALID_SEEL_COPYEE, Aquarium_Window_SeelCopyee_HumanRight)
		waitmovement(0)
		applymovement(LOCALID_SEEL_COPYER, Aquarium_Window_SeelCopyee_SeelLeft)
		applymovement(LOCALID_SEEL_COPYEE, Aquarium_Window_SeelCopyee_HumanLeft)
	} else {
		applymovement(LOCALID_SEEL_COPYER, Aquarium_Window_SeelCopyee_SeelLeft)
		applymovement(LOCALID_SEEL_COPYEE, Aquarium_Window_SeelCopyee_HumanLeft)
		waitmovement(0)
		applymovement(LOCALID_SEEL_COPYER, Aquarium_Window_SeelCopyee_SeelRight)
		applymovement(LOCALID_SEEL_COPYEE, Aquarium_Window_SeelCopyee_HumanRight)
	}
	waitmovement(0)
	applymovement(LOCALID_SEEL_COPYEE, Aquarium_Window_SeelCopyee_FacePlayerDelayed)
	waitmovement(0)
	msgbox(format("I didn't think Seel were\nknown for mimicry."), MSGBOX_NPC)
	waitmovement(0)
	applymovement(LOCALID_SEEL_COPYEE, Common_Movement_FaceOriginalDirection)
	waitmovement(0)
	releaseall
	end
}

movement Aquarium_Window_SeelCopyee_SeelLeft {
	delay_4
	walk_left
	walk_in_place_down
	// XXX: face_down isn't working on seel
	step_end
}
movement Aquarium_Window_SeelCopyee_SeelRight {
	delay_4
	walk_right
	walk_in_place_down
	// XXX: face_down isn't working on seel
	step_end
}
movement Aquarium_Window_SeelCopyee_HumanLeft {
	walk_left
	face_up
	delay_16
	delay_16
	step_end
}
movement Aquarium_Window_SeelCopyee_HumanRight {
	walk_right
	face_up
	delay_16
	delay_16
	step_end
}
movement Aquarium_Window_SeelCopyee_FacePlayerDelayed {
	delay_16
	face_player
	step_end
}

script Aquarium_Window_RunningCirclesBoy {
	msgbox(
		format("I saw a picture of that purple spikey thing in one of my textbooks!"),
		MSGBOX_NPC)
	end
}

script Aquarium_Window_Maniac {
	msgbox(
		format(
			"That strain of seaweed thrives in warmer waters "
			"while Seel thrive in colder waters.\p"
			"I will send a strongly worded letter about this."
		),
		MSGBOX_NPC)
	applymovement(VAR_LAST_TALKED, Common_Movement_FaceOriginalDirection)
	end
}

script Aquarium_Window_Lass {
	msgbox(
		format(
			"Do you think those creatures are aware that someone is watching them?\p"
			"Do you think we'd know if someone were watching us?"
		),
		MSGBOX_NPC)
	applymovement(VAR_LAST_TALKED, Common_Movement_FaceOriginalDirection)
	end
}

script Aquarium_Window_Refraction {
	msgbox(
		format(
			"The way the light hits the top of the water looks really pretty from below."
		),
		MSGBOX_NPC)
	applymovement(VAR_LAST_TALKED, Common_Movement_FaceOriginalDirection)
	end
}

script Aquarium_Window_Grunt {
	lockall
	faceplayer
	msgbox(
		format("No! You weren't supposed to show up while we were still here!"),
		MSGBOX_DEFAULT)
	trainerbattle_no_intro(
		TRAINER_GRUNT_WINDOW,
		"Overwhelming",
	)
	if (flag(FLAG_HIDE_BIOLUM_GRUNT))
	{
		msgbox(
			format(
				"You want the key to the room that the boss has locked himself in?\p"
				"Fine. Take it."),
			MSGBOX_DEFAULT)
		giveitem(ITEM_ROOM_1_KEY)
	} else {
		msgbox(
			format(
				"You want the key to the room that the boss has locked himself in?\p"
				"I don't have it. So there!"),
			MSGBOX_DEFAULT)
	}
	closemessage
	if (var(VAR_RESULT) == DIR_NORTH) {
		applymovement(LOCALID_GRUNT, Aquarium_Window_Grunt_Flee_North)
	} else {
		applymovement(LOCALID_GRUNT, Aquarium_Window_Grunt_Flee_Other)
	}
	waitmovement(0)
	removeobject(LOCALID_GRUNT)
	setflag(FLAG_HIDE_WINDOW_GRUNT)
	playse(SE_DOOR)
	releaseall
	end
}

movement Aquarium_Window_Grunt_Flee_North {
	walk_right
	walk_down * 5
	walk_right
	walk_down
}
movement Aquarium_Window_Grunt_Flee_Other {
	walk_down * 2
	walk_right
	walk_down * 3
	walk_right
	walk_down
}
