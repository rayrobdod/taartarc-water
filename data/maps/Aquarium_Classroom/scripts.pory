const OBJ_ID_LEADER = 1
const OBJ_ID_DIRECTOR = 2


mapscripts Aquarium_Classroom_MapScripts {}

script Aquarium_Classroom_FightLeader_RightLeft {
	call(Aquarium_Classroom_FightLeader_BeforePlayerMove)
	applymovement(OBJ_EVENT_ID_PLAYER, Aquarium_Classroom_FightLeader_RightLeft_Movement)
	waitmovement(0)
	msgbox(Aquarium_Classroom_NewlineEllipsis)
	applymovement(OBJ_ID_LEADER, Common_Movement_WalkInPlaceRight)
	call(Aquarium_Classroom_FightLeader_AfterPlayerMove)
}

script Aquarium_Classroom_FightLeader_RightUp {
	call(Aquarium_Classroom_FightLeader_BeforePlayerMove)
	applymovement(OBJ_EVENT_ID_PLAYER, Aquarium_Classroom_FightLeader_RightUp_Movement)
	waitmovement(0)
	msgbox(Aquarium_Classroom_NewlineEllipsis)
	applymovement(OBJ_ID_LEADER, Common_Movement_WalkInPlaceRight)
	call(Aquarium_Classroom_FightLeader_AfterPlayerMove)
}

script Aquarium_Classroom_FightLeader_Left {
	call(Aquarium_Classroom_FightLeader_BeforePlayerMove)
	applymovement(OBJ_EVENT_ID_PLAYER, Aquarium_Classroom_FightLeader_Left_Movement)
	waitmovement(0)
	msgbox(Aquarium_Classroom_NewlineEllipsis)
	applymovement(OBJ_ID_LEADER, Common_Movement_WalkInPlaceLeft)
	call(Aquarium_Classroom_FightLeader_AfterPlayerMove)
}





script(local) Aquarium_Classroom_FightLeader_BeforePlayerMove {
	lockall
	applymovement(OBJ_ID_LEADER, Common_Movement_WalkInPlaceDown)
	msgbox(Aquarium_Classroom_Ellipsis)
	applymovement(OBJ_ID_DIRECTOR, Aquarium_Classroom_WalkInPlaceUp)
	msgbox(Aquarium_Classroom_NewlineEllipsis)
	applymovement(OBJ_ID_LEADER, Common_Movement_WalkInPlaceDown)
	msgbox(format("I am known to be patient.\nI can wait all day."))
	applymovement(OBJ_ID_DIRECTOR, Aquarium_Classroom_WalkInPlaceUp)
	msgbox(format("You're the one that keeps\Nbreaking the silence."))
	applymovement(OBJ_ID_LEADER, Common_Movement_WalkInPlaceDown)
	msgbox(Aquarium_Classroom_Ellipsis)
}

script(local) Aquarium_Classroom_FightLeader_AfterPlayerMove {
	msgbox(format(
		"I should have been less patient.\p"
		"I was hoping to avoid your attention with a covert operation, "
		"but I'm not going to back down just because you showed up."))
	trainerbattle_no_intro(TRAINER_LEADER, "Foiled again.")
	msgbox(format("Fine. We're going. Keep your Aqueous Orb. We can figure out how to advance our plans without that orb."))
	closemessage
	applymovement(OBJ_ID_LEADER, Aquarium_Classroom_FightLeader_LeaderFlee)
	waitmovement(0)
	removeobject(OBJ_ID_LEADER)
	setflag(FLAG_HIDE_CLASSROOM_LEADER)
	playse(SE_DOOR)

	applymovement(OBJ_ID_DIRECTOR, Aquarium_Classroom_FightLeader_Director_WalkUp)
	applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_FaceDown)
	msgbox(format("Thank you for saving us."))
	setrespawn(HEAL_LOCATION_GARDENS)
	fadescreenspeed(FADE_TO_BLACK, 12)
	special(GameClear)
	waitstate
	releaseall
}

movement Aquarium_Classroom_WalkInPlaceUp {
	walk_in_place_up
}

movement Aquarium_Classroom_FightLeader_RightLeft_Movement {
	walk_up
	walk_left
	walk_up
	walk_left
	walk_up
	walk_left
}

movement Aquarium_Classroom_FightLeader_RightUp_Movement {
	walk_left
	walk_up
	walk_left
	walk_up
	walk_left
	walk_left
}

movement Aquarium_Classroom_FightLeader_Left_Movement {
	walk_up
	walk_right
	walk_up
	walk_right
	walk_up
	walk_right
}

movement Aquarium_Classroom_FightLeader_LeaderFlee {
	walk_fast_down
	walk_fast_right * 5
	walk_fast_down * 2
	face_right
	delay_8
}

movement Aquarium_Classroom_FightLeader_Director_WalkUp {
	delay_16
	walk_up
}

raw `
Aquarium_Classroom_NewlineEllipsis:
	.string "\n"
Aquarium_Classroom_Ellipsis:
	.string "â€¦$"
`
