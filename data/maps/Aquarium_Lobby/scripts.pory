// VAR_LOBBY_STATE
//	0 -> 1 : receive briefing and map

const OBJ_ID_ATTENDANT = 1


mapscripts Aquarium_Lobby_MapScripts {
}

script Aquarium_Lobby_Attendant {
	if (! flag(FLAG_HIDE_WINDOW_GRUNT) && ! flag(FLAG_HIDE_BIOLUM_GRUNT)) {
		msgbox(
			format(
				"You'll want to start by getting the community room key back from the grunts.\N"
				"I know they haven't left yet, so they must still be in here somewhere."
			),
			MSGBOX_NPC)
	}
	elif (! flag(FLAG_HIDE_WINDOW_GRUNT) || ! flag(FLAG_HIDE_BIOLUM_GRUNT)) {
		msgbox(
			format(
				"There were only two grunts; if one didn't have the key, the other must have it."
			),
			MSGBOX_NPC)
	}
	else {
		msgbox(
			format(
				"You have the community room key? Great!\N"
				"Use the machine in the storage room to heal if you need it, "
				"then go in the community room and kick out the team leader."
			),
			MSGBOX_NPC)
	}
	end
}

script Aquarium_Lobby_Briefing_FarLeft {
	call(Aquarium_Lobby_Briefing_BeforePlayerMove)
	applymovement(OBJ_EVENT_ID_PLAYER, Aquarium_Lobby_Movement_WalkRight3)
	call(Aquarium_Lobby_Briefing_AfterPlayerMove)
}
script Aquarium_Lobby_Briefing_Left {
	call(Aquarium_Lobby_Briefing_BeforePlayerMove)
	applymovement(OBJ_EVENT_ID_PLAYER, Aquarium_Lobby_Movement_WalkRight2)
	call(Aquarium_Lobby_Briefing_AfterPlayerMove)
}
script Aquarium_Lobby_Briefing_Right {
	call(Aquarium_Lobby_Briefing_BeforePlayerMove)
	applymovement(OBJ_EVENT_ID_PLAYER, Aquarium_Lobby_Movement_WalkRight)
	call(Aquarium_Lobby_Briefing_AfterPlayerMove)
}
script Aquarium_Lobby_Briefing_FarRight {
	call(Aquarium_Lobby_Briefing_BeforePlayerMove)
	applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_FaceRight)
	call(Aquarium_Lobby_Briefing_AfterPlayerMove)
}

script(local) Aquarium_Lobby_Briefing_BeforePlayerMove {
	lockall
	applymovement(OBJ_ID_ATTENDANT, Common_Movement_FaceLeft)
	msgbox(format("Hello."), MSGBOX_DEFAULT)
	applymovement(OBJ_ID_ATTENDANT, Common_Movement_FaceUp)
	return
}

script(local) Aquarium_Lobby_Briefing_AfterPlayerMove {
	waitmovement(0)
	msgbox(format("Welcome to the Cerulean Aquarium."), MSGBOX_DEFAULT)
	applymovement(OBJ_ID_ATTENDANT, Common_Movement_FaceLeft)
	msgbox(
		format("Oh! Champion Lance!\NThank you for coming!\p"
			"I'm not sure how much you were already told so…\p"
			"The Team Torpedo leader from the wanted posters showed up "
			"and demanded to speak to the director.\p"
			"The leader took the director into the community room at the far side of the lobby "
			"and the grunts locked them in \N“so they won’t be disturbed”"
			"and then explicitly told us to not call for help.\p"
			"The grunts haven't left and should still have the key.\p"
			"So, get the key back and then save the director."),
		MSGBOX_DEFAULT)
	applymovement(OBJ_ID_ATTENDANT, Common_Movement_WalkInPlaceRight)
	msgbox(
		format("We do have a healing machine this storage room back here if you need it,"),
		MSGBOX_DEFAULT)
	applymovement(OBJ_ID_ATTENDANT, Common_Movement_WalkInPlaceLeft)
	msgbox(
		format("and here's a map of the Aquarium."),
		MSGBOX_DEFAULT)
	giveitem(ITEM_TOWN_MAP)
	msgbox(
		format("Thank you in advance."),
		MSGBOX_DEFAULT)
	releaseall
	setvar(VAR_LOBBY_STATE, 1)
	end
}

raw `
Aquarium_Lobby_Movement_WalkRight3:
	walk_right
Aquarium_Lobby_Movement_WalkRight2:
	walk_right
Aquarium_Lobby_Movement_WalkRight:
	walk_right
	step_end
`
