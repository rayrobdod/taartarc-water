const LOCALID_GRUNT = 4

mapscripts Aquarium_Bioluminescence_MapScripts {}



script Aquarium_Bioluminescence_Hypnotized {
	applymovement(VAR_LAST_TALKED, Common_Movement_Delay32)
	waitmovement(0)
	msgbox(
		format("This person seems hypnotized by the Chinchou's lights."),
		MSGBOX_SIGN)
	end
}

script Aquarium_Bioluminescence_QuestioningVolbeat {
	msgbox(
		format(
			"Volbeat does glow, but I'm not sure whether it is supposed to be here."
		),
		MSGBOX_NPC)
	applymovement(VAR_LAST_TALKED, Common_Movement_FaceOriginalDirection)
	end
}

script Aquarium_Bioluminescence_RedLight {
	msgbox(
		format(
			"Water absorbs red light, making water look blue normally, right?\N"
			"So, if this room only has red light, does that make inside the water pitch-black?"
		),
		MSGBOX_NPC)
	applymovement(VAR_LAST_TALKED, Common_Movement_FaceOriginalDirection)
	end
}

script Aquarium_Bioluminescence_Volbeat {
	lockall
	specialvar(VAR_RESULT, GetPlayerFacingDirection)
	waitse
	playmoncry(SPECIES_VOLBEAT, CRY_MODE_NORMAL)
	switch (var(VAR_RESULT)) {
		case DIR_NORTH:
			applymovement(VAR_LAST_TALKED, Aquarium_Bioluminescence_Volbeat_North)
		case DIR_EAST:
			applymovement(VAR_LAST_TALKED, Aquarium_Bioluminescence_Volbeat_East)
		case DIR_WEST:
			applymovement(VAR_LAST_TALKED, Aquarium_Bioluminescence_Volbeat_West)
		default:
			applymovement(VAR_LAST_TALKED, Aquarium_Bioluminescence_Volbeat_South)
	}
	waitmovement(0)
	releaseall
	end
}

movement Aquarium_Bioluminescence_Volbeat_North {
	walk_in_place_down
	delay_8
	jump_in_place_up
	walk_in_place_up * 4
	walk_in_place_down
}
movement Aquarium_Bioluminescence_Volbeat_South {
	walk_in_place_up
	delay_8
	jump_in_place_down
	walk_in_place_down * 4
	walk_in_place_up
}
movement Aquarium_Bioluminescence_Volbeat_East {
	walk_in_place_left
	delay_8
	jump_in_place_right
	walk_in_place_right * 4
	walk_in_place_left
}
movement Aquarium_Bioluminescence_Volbeat_West {
	walk_in_place_right
	delay_8
	jump_in_place_left
	walk_in_place_left * 4
	walk_in_place_right
}
